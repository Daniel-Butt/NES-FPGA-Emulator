library ieee;
use ieee.std_logic_1164.all;

package PPU_CONTROL_REGS is 
	type PPU_REGS is array(0 to 7) of std_logic_vector(7 downto 0);
	constant PPU_CTRL_IDX 	: integer range 0 to 7 := 0;
	constant PPU_MASK_IDX 	: integer range 0 to 7 := 1;
	constant PPU_STATUS_IDX : integer range 0 to 7 := 2;
	constant OAM_ADDR_IDX 	: integer range 0 to 7 := 3;
	constant OAM_DATA_IDX	: integer range 0 to 7 := 4;
	constant PPU_SCROLL_IDX : integer range 0 to 7 := 5;
	constant PPU_ADDR_IDX 	: integer range 0 to 7 := 6;
	constant PPU_DATA_IDX 	: integer range 0 to 7 := 7;
	
end package;

library ieee;
use ieee.std_logic_1164.all;

package OTHER_CONTROL_REGS is 
	type OTHER_REGS is array(0 to 31) of std_logic_vector(7 downto 0);
	constant OAM_DMA_IDX 	: integer range 0 to 31 := 20;
end package;

library ieee;
use ieee.std_logic_1164.all;
use ieee.std_logic_unsigned.all;
USE ieee.numeric_std.ALL;
use WORK.PPU_CONTROL_REGS.all;
use WORK.OTHER_CONTROL_REGS.all;

entity MemoryController is 
	port (
		
		PC_ADDR				: in std_logic_vector(15 downto 0);	
		RAM_ADDR 			: in std_logic_vector(15 downto 0) := x"0000";	
		RAM_DATA_IN 		: in std_logic_vector(7 downto 0);
		
		W						: in std_logic; -- 1 write
		R						: in std_logic; -- 0 PC, 1 RAM
		FCLK 					: in std_logic;
		
		HEX_OUT 				: out std_logic_vector(7 downto 0);
		RAM_DATA_OUT		: out std_logic_vector(7 downto 0);
		PPU_CTRL_OUT 		: out PPU_REGS;
		PPU_STATUS_in		: in std_logic_vector(7 downto 0);
		OTHER_CTRL_OUT		: out OTHER_REGS;
		HC_IN					: in std_logic_vector(7 downto 0);
		DMA_WRITE			: out std_logic
	
	);
end MemoryController;


architecture rtl of MemoryController is	

	attribute ram_init_file : string;
	
   type MEM_Array is array (0 to 65535) of std_logic_vector(7 downto 0); -- x4020 - xFFFF
   signal MEM : MEM_Array;
	attribute ram_init_file of MEM : signal is "multi_frame_test.mif"; --"games/Donkey_Kong.mif"; -- replace with game ROM
	

	signal readAddr 	: unsigned(15 downto 0) := to_unsigned(0, 16);
	
	signal PPU_CTRL 	: PPU_REGS;
	signal OTHER_CTRL	: OTHER_REGS;
	
	signal OAM_TRANSFER 	: std_logic := '0';
	signal OAM_COUNT 		: unsigned (8 downto 0) := to_unsigned(0, 9);
	
	
	procedure writeMem(signal addr : in std_logic_vector(15 downto 0)) is	
	begin
		
		if (addr < x"2000") then
			-- CPU RAM
			MEM(to_integer(unsigned(addr(10 downto 0)))) <= RAM_DATA_IN;
			
			if(addr = x"FF") then
				HEX_OUT <= RAM_DATA_IN;
			end if;
			
		elsif (addr < x"4000") then
		
			-- PPU registers
			PPU_CTRL(to_integer(unsigned(addr(2 downto 0)))) <= RAM_DATA_IN;
			
		elsif (addr < x"4020") then
			-- IO/APU registers
			
			if (addr = x"4014") then
				OAM_TRANSFER <= '1';
			end if;
			
			if (addr = x"4013") then
				OTHER_CTRL(to_integer(unsigned(addr(4 downto 0)))) <= HC_IN;
			else
				OTHER_CTRL(to_integer(unsigned(addr(4 downto 0)))) <= RAM_DATA_IN;
			end if;
		
		else
			-- Cartridge space
			MEM(to_integer(unsigned(addr))) <= RAM_DATA_IN;
		
		end if;
		

	end writeMem;
	
	
	procedure readMem(constant addr : in std_logic_vector(15 downto 0)) is
	begin
	
		
		if (addr < x"2000") then
			-- CPU RAM
			readAddr <= "00000" & unsigned(addr(10 downto 0));
			
		elsif	(addr < x"4000") then
		-- PPU registers
			readAddr <= x"200" & "0" & unsigned(addr(2 downto 0));
			
		elsif (addr < x"4020") then
			-- IO/APU registers
			readAddr <= x"40" & "000" & unsigned(addr(4 downto 0));
			
		else
			-- Cartridge space
			readAddr <= unsigned(addr);
		
		end if;
	
	end readMem;
	
	
begin

	process(FCLK, PPU_STATUS_in)
		variable addr : std_logic_vector(15 downto 0);
	begin
		
		if (PPU_STATUS_in(7 downto 5) /= "000") then
			PPU_CTRL(PPU_STATUS_IDX)(7 downto 5) <= PPU_CTRL(PPU_STATUS_IDX)(7 downto 5) or PPU_STATUS_in(7 downto 5);
		
		elsif(falling_edge(FCLK)) then
			
			
			if (OAM_TRANSFER = '1') then
				OAM_COUNT <= OAM_COUNT + 1;
				addr := OTHER_CTRL(OAM_DMA_IDX) & PPU_CTRL(OAM_ADDR_IDX);
				
				if (OAM_COUNT = x"FF") then
					OAM_TRANSFER <= '0';
				end if;
				
				if (OAM_COUNT(0) = '0') then
					DMA_WRITE <= '0';
					
				else
					DMA_WRITE <= '1';
					PPU_CTRL(OAM_ADDR_IDX) <= std_logic_vector(unsigned(PPU_CTRL(OAM_ADDR_IDX) + 1));
				end if;
				
			else
			
				addr := PC_ADDR;
				
				-- Write
				if(W = '1') then
				
					writeMem(RAM_ADDR);
					
				-- Read
				elsif (R = '1') then
				
					addr := RAM_ADDR;
					
				end if;
				
				
			end if;
			
			readMem(addr);
		
		end if;
		
	end process;
	
	process(readAddr, MEM, PPU_CTRL, OTHER_CTRL)
	begin
		
		
		if (readAddr < x"2000") then
			-- CPU RAM
			RAM_DATA_OUT <= MEM(to_integer(readAddr));
			
		elsif	(readAddr < x"4000") then
			-- PPU registers
			RAM_DATA_OUT <= PPU_CTRL(to_integer(readAddr(2 downto 0)));
			
		elsif (readAddr < x"4020") then
			-- IO/APU registers
			RAM_DATA_OUT <= OTHER_CTRL(to_integer(readAddr(4 downto 0)));
			
		else
			-- Cartridge space
			RAM_DATA_OUT <= MEM(to_integer(readAddr));
		
		end if;
			
	end process;
	
	PPU_CTRL_OUT <= PPU_CTRL;
	OTHER_CTRL_OUT <= OTHER_CTRL;
	
end rtl;




























